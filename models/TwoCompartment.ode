# From https://www.embopress.org/doi/full/10.15252/msb.20198870
# Stimulus Parameter is LT
# Renamed LRPMa to just LRPM

# init RPMa = 0.0, RPMi = 1.0, PDNFa = 0.5, LRPM = 0.0, REa = 0.0, REi = 0.0, LREi = 0.0
init RPMa = 0.001, RPMi = 0.912, PDNFa = 0.654, LRPM = 0.0, REa = 0.0, REi = 0.087, LREi = 0.0

# equations
dRPMa/dt = R_T*RPMi*(alpha1*RPMi + alpha2*RPMa + alpha3*LRPM) - gamma_DNF*PDNFT*PDNFa*RPMa - gamma_NR*P_NRT*RPMa - k_in*RPMa - k_on*RPMa*LT + 0.5*k_off*LRPM
dRPMi/dt = -R_T*RPMi*(alpha1*RPMi + alpha2*RPMa + alpha3*LRPM) + gamma_DNF*PDNFT*PDNFa*RPMa + gamma_NR*P_NRT*RPMa - ki_in*k_in*RPMi + k_rec*REi - k_on*RPMi*LT + 0.5*k_off*LRPM
dPDNFa/dt = k_1*((1-PDNFa) - k_21*PDNFa - beta_DNF*PDNFa*R_T*(RPMa + LRPM))
dLRPM/dt = k_on*(RPMa+RPMi)*LT - k_off*LRPM - k_deg*k_in*LRPM
dREa/dt = k_in*RPMa - gamma_NF*P_NFT*REa
dREi/dt = ki_in*k_in*RPMi + gamma_NF*P_NFT*REa - k_rec*REi
dLREi/dt = k_deg*k_in*LRPM

# parameters
#k_deg was set to 0 for Magenta profile in Fig 5B.
param gamma_DNF=3.0, gamma_NF=3.0, gamma_NR=0.001, beta_DNF=36.0558, k_in=0.02, k_rec=0.042, ki_in=0.2, k_deg=0.2
param R_T=1.3499, LT=0.2926, PDNFT=1.0, P_NFT=1.0, P_NRT=1.0
param alpha1=0.0017, alpha2=0.3, alpha3=1.0, k_1=0.01, k_21=0.5, k_on=0.003, k_off=0.01668

@ meth=cvode,dt=.1,total=1500,maxstor=100000
@ xp=t,bound=1000000
@ xlo=0,xhi=1500,ylo=0,yhi=2
â€‹
# AUTO parameters
@ autovar=x,autoxmin=0.1,autoymin=0.0,autoxmax=5,autoymax=1
@ ntst=1000,nmax=2500,npr=500,ds=0.001,dsmin=0.0001,dsmax=0.05
@ ncol=4,epsl=1e-4,parmin=0,parmax=5,normmin=0,normmax=1000
@ epsu=1e-4,epss=0.0001

done